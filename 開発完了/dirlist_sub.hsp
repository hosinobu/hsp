#ifndef dirlist_sub
#module

//カッコ内はデフォルト
//dirlist_sub 結果を格納する文字列型変数, 検索するフォルダ(カレントディレクトリ), ファイルマスク(*.*), 検索モード(0), 結果を格納する変数の初期サイズ(1)

#define global dirlist_sub(%1, %2 = dir_cur, %3 = "*.*", %4 = 1, %5 = 1) nd@dirlistsub = dir_cur: sdim %1, %5: __dirlist_sub %1, %2, %3, %4: chdir nd@dirlistsub

#deffunc __dirlist_sub array 結果, str 作業フォルダ名, str filemask, int p2, local z, local k

	//作業フォルダの変更
	n = 作業フォルダ名 : if instr(n,,"?") >= 0: return ; フォルダ名が文字化けしている
	chdir 作業フォルダ名

	//Windowsフォルダは無視
	n = dir_cur
	if peek(n) >= 'a' && peek(n) <= 'z'{
		poke n,,peek(n) + 'A'-'a'
	}
	if n == dir_win: return


	//---このフォルダ内の作業

	dirlist z, filemask, p2
	
	notesel z
	repeat stat
	
		noteget t, cnt
		結果 += dir_cur + "\\"+ t +"\n"
		
	loop

	//---ここまで


	//★★　サブディレクトリに再帰（この関数を繰り返す）
	
	dirlist z, "*", 6 ; 隠し属性やシステム属性のディレクトリには移動しない
	dirlist k, "*", 5 

	repeat stat
	
		notesel k
		noteget t, cnt

		notesel z
		if notefind(t) == -1{
			__dirlist_sub 結果, 作業フォルダ名 + "\\" + t, filemask, p2
		}
		
	loop

	//★★　ここまで

	await

	return

#global
#endif