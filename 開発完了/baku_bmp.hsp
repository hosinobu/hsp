#ifndef baku_animation_mod
#module baku_animation_mod \
x, y, type, counter
#modinit double _x, double _y, int _type
	x = _x
	y = _y
	type = _type
	return
#modfunc baku_animation
	if counter >= 16: delmod thismod: return
	pos x, y
	celput baku_buf, 16 * type + counter
	counter++
	return
/////////////////////////////////////////////////////////・
/////////////////////////////////////////////////////////・
#deffunc add_baku_animation double _x, double _y, int _type
	newmod baku_animation_container, baku_animation_mod, _x, _y, _type
	return
#deffunc baku_animation_all
	gmode 2
	foreach baku_animation_container
		baku_animation baku_animation_container(cnt)
	loop
	return
#deffunc init_baku_animation int center_point
	tmp = ginfo(3)
	if baku_buf = 0{
		baku_buf = ginfo_newid
		celload dir_tv + "baku.bmp", baku_buf
	}
	gsel baku_buf
	if center_point{
		celdiv baku_buf, ginfo_sx/8, ginfo_sy/8, 0          ,           0 ; 左上座標
	}else{
		celdiv baku_buf, ginfo_sx/8, ginfo_sy/8, ginfo_sx/16, ginfo_sy/16 ; 中心座標
	}
	gsel tmp
	dimtype baku_animation_container, 5
	return
#global
init_baku_animation
#endif


/*

init_baku_animation p1
p1に0を指定すると座標を【中心座標】として描画するようになります(デフォルトなので中心座標でよければこの命令は不要です)
p1に1を指定すると座標を【左上座標】として描画するようになります

add_baku_animation x, y, type
x,yにアニメーションを登録します。　typeは0-3までの四種類あります。

baku_animation_all
メインループの描画部に配置しておきます。

*/
#if 0

*main

	if rnd(10) = 0{; たまにアニメーションを追加
		add_baku_animation  rnd(ginfo_sx), rnd(ginfo_sy), rnd(4)
	}


	redraw 2 ; 描画開始
	color:boxf ;　背景初期化
	
	baku_animation_all
	
	redraw ; 描画終了
	await 16
goto*main
#endif
