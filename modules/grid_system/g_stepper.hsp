#include "area_grid_system.hsp"
#include "../keyfps.hsp"
#module grid_stepper area_number, v, fit, ps
#modinit var value, int _area
	dup v, value
	area_number = _area
	
	set_fitmode_g_stepper thismod, 1
	return


#modfunc set_fitmode_g_stepper int _mode
	fit = _mode
	return
#modfunc set_position_g_stepper int _x, int _y
	ps = _x, _y
	return
#modfunc set_size_g_stepper int _sizex, int _sizey
	ps(2) = _sizex, _sizey
	return
	
#modfunc draw_g_stepper

	thisrect thismod, oya, area_number
	get_rect_by_fitmode fit, oya, ps, rect
	
	color
	boxf rect,rect.1,rect.2,rect.3
	color 255,255,255
	pos rect,rect.1: mes v

	color 180,180,180


	half_y = (rect.3 - rect.1) / 2
	uprect   = rect.2 - half_y, rect.1         , rect.2 , rect.3 - half_y
	downrect = rect.2 - half_y, rect.3 - half_y, rect.2 , rect.3
	if _getkey(1) == 1{
		if in_rect(uprect): v++
		if in_rect(downrect): v--
		v = limit(v,0)
	}
	color 255,30,30
	aboxfl uprect, 1
	aboxfl downrect, 1
	return

//各モジュールで【矩形エリア】モックを利用するためのコード。コピペ推奨
#modfunc local thisrect array mocklist, int area_number_mock
	if mock_flag@global{
		get_rect_mock mockrect //モックから矩形取得
		mocklist(0) = mockrect(0,area_number_mock)
		mocklist(1) = mockrect(1,area_number_mock)
		mocklist(2) = mockrect(2,area_number_mock)
		mocklist(3) = mockrect(3,area_number_mock) 
	}else{
		get_rect area_number_mock, mocklist //グリッドシステムからエリア番号で矩形取得
	}
	return


#defcfunc local in_rect array a
	return a(0) <= mousex && a(1) <= mousey && a(2) >= mousex && a(3) >= mousey
#global

#if 1
mock_flag@global = 1


set_rect_mock 90, 0,0,60,30
set_rect_mock 30, 60,60, 100,100

newmod c, grid_stepper, a, 90


*main
	manage_keyfps
	foreach c
	draw_g_stepper c(cnt)
	loop
	redraw:redraw 2
	goto*main
#endif